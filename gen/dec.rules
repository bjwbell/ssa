// Copyright 2016 The Go Authors. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

// This file contains rules to decompose builtin compound types
// (complex,string,slice,interface) into their constituent
// types.  These rules work together with the decomposeBuiltIn
// pass which handles phis of these types.

// complex ops
(ComplexReal (ComplexMake real _  )) -> real
(ComplexImag (ComplexMake _ imag )) -> imag

(Load <t> ptr mem) && t.IsComplex() && t.Size() == 8 ->
  (ComplexMake
    (Load <fe.TypeFloat32()> ptr mem)
    (Load <fe.TypeFloat32()>
      (OffPtr <fe.TypeFloat32().PtrTo()> [4] ptr)
      mem)
    )
(Store {t} dst (ComplexMake real imag) mem) && t.(Type).Size() == 8 ->
  (Store {fe.TypeFloat32()}
    (OffPtr <fe.TypeFloat32().PtrTo()> [4] dst)
    imag
    (Store {fe.TypeFloat32()} dst real mem))
(Load <t> ptr mem) && t.IsComplex() && t.Size() == 16 ->
  (ComplexMake
    (Load <fe.TypeFloat64()> ptr mem)
    (Load <fe.TypeFloat64()>
      (OffPtr <fe.TypeFloat64().PtrTo()> [8] ptr)
      mem)
    )
(Store {t} dst (ComplexMake real imag) mem) && t.(Type).Size() == 16 ->
  (Store {fe.TypeFloat64()}
    (OffPtr <fe.TypeFloat64().PtrTo()> [8] dst)
    imag
    (Store {fe.TypeFloat64()} dst real mem))

// string ops
(StringPtr (StringMake ptr _)) -> ptr
(StringLen (StringMake _ len)) -> len

(Load <t> ptr mem) && t.IsString() ->
  (StringMake
    (Load <fe.TypeBytePtr()> ptr mem)
    (Load <fe.TypeInt()>
      (OffPtr <fe.TypeInt().PtrTo()> [config.PtrSize] ptr)
      mem))
(Store dst (StringMake ptr len) mem) ->
  (Store {fe.TypeInt()}
    (OffPtr <fe.TypeInt().PtrTo()> [config.PtrSize] dst)
    len
    (Store {fe.TypeBytePtr()} dst ptr mem))

// slice ops
(SlicePtr (SliceMake ptr _ _ )) -> ptr
(SliceLen (SliceMake _ len _)) -> len
(SliceCap (SliceMake _ _ cap)) -> cap

(Load <t> ptr mem) && t.IsSlice() ->
  (SliceMake
    (Load <t.ElemType().PtrTo()> ptr mem)
    (Load <fe.TypeInt()>
      (OffPtr <fe.TypeInt().PtrTo()> [config.PtrSize] ptr)
      mem)
    (Load <fe.TypeInt()>
      (OffPtr <fe.TypeInt().PtrTo()> [2*config.PtrSize] ptr)
      mem))
(Store dst (SliceMake ptr len cap) mem) ->
  (Store {fe.TypeInt()}
    (OffPtr <fe.TypeInt().PtrTo()> [2*config.PtrSize] dst)
    cap
    (Store {fe.TypeInt()}
      (OffPtr <fe.TypeInt().PtrTo()> [config.PtrSize] dst)
      len
      (Store {fe.TypeBytePtr()} dst ptr mem)))

// interface ops
(ITab (IMake itab _)) -> itab
(IData (IMake _ data)) -> data

(Load <t> ptr mem) && t.IsInterface() ->
  (IMake
    (Load <fe.TypeBytePtr()> ptr mem)
    (Load <fe.TypeBytePtr()>
      (OffPtr <fe.TypeBytePtr().PtrTo()> [config.PtrSize] ptr)
      mem))
(Store dst (IMake itab data) mem) ->
  (Store {fe.TypeBytePtr()}
    (OffPtr <fe.TypeBytePtr().PtrTo()> [config.PtrSize] dst)
    data
    (Store {fe.TypeUintptr()} dst itab mem))
